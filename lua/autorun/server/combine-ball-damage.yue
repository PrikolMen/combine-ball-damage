addonName = "Combine Ball Damage"
className = "prop_combine_ball"

DMG_DISSOLVE = DMG_DISSOLVE
:band, :bor = bit
:Simple = timer

CVAR_FLAGS = bor( FCVAR_ARCHIVE, FCVAR_NOTIFY )
Damage = CreateConVar( "mp_combine_ball_damage", "10000", CVAR_FLAGS, "Sets the amount of damage caused by '#{className}'.", 15, 1000000 )
PlayerProtection = CreateConVar( "mp_combine_ball_player_protection", "0", CVAR_FLAGS, "Protects players against damage from his own '#{className}'.", 0, 1 )
NPCProtection = CreateConVar( "mp_combine_ball_npc_protection", "0", CVAR_FLAGS, "Protects NPCs against damage from his own '#{className}'.", 0, 1 )

hook.Add "OnEntityCreated", addonName, =>
    if @GetClass! ~= className
        return

    Simple 0, ->
        unless @IsValid!
            return

        owner = @GetOwner!
        if not owner\IsValid! or ( PlayerProtection\GetBool! and owner\IsPlayer! ) or ( NPCProtection\GetBool! and owner\IsNPC! )
            return

        @[ addonName ] = owner
        @SetOwner!

hook.Add "EntityTakeDamage", addonName, ( damageInfo ) =>
    entity = damageInfo\GetInflictor!
    unless entity\IsValid! and entity\GetClass! == className
        return

    owner = entity[ addonName ]
    if owner and owner\IsValid!
        damageInfo\SetAttacker( owner )
        entity\SetOwner( owner )

        Simple 0, ->
            if entity\IsValid!
                entity\SetOwner!

    if band( damageInfo\GetDamageType!, DMG_DISSOLVE ) ~= DMG_DISSOLVE
        damageInfo\SetDamageType( bor( damageInfo\GetDamageType!, DMG_DISSOLVE ) )

    damageInfo\SetDamage( Damage\GetInt! )
